apply plugin: 'maven-publish'
apply plugin: 'signing'

// https://developer.android.com/build/publish-library/configure-pub-variants
android {
    publishing {
        singleVariant("release") {
        }
    }
}

afterEvaluate {

    task androidJavadocs(type: Javadoc) {
        source = "src/main/java"
        android.libraryVariants.all { variant ->
            classpath += files("build/generated/source/aidl/${variant.name}")
            classpath += files("build/generated/source/r/${variant.name}")
            classpath += files("build/generated/source/buildConfig/${variant.name}")
            if (variant.name == 'release') {
                owner.classpath += variant.javaCompiler.classpath
            }
        }
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        archiveClassifier.set('javadoc')
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from "src/main/java"
    }

    publishing {
        publications {
            release(MavenPublication) {
                groupId GROUP
                artifactId POM_ARTIFACT_ID
                version VERSION_CODE

                from components.release

                artifact androidSourcesJar
                artifact androidJavadocsJar

                pom {
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    url = POM_URL
                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                            distribution = POM_LICENCE_DIST
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                        }
                    }
                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }
                }
            }
        }

        repositories {
            maven {
                name "sonatype"
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username project.nexusUsername
                    password project.nexusPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

model {
    tasks.androidJavadocs {
        dependsOn(project.tasks.build)
    }
}